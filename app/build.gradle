apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'

android {
    packagingOptions {
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    compileSdkVersion 28

    defaultConfig {
        applicationId "dev.ezorrio.phoenix"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 10500
        versionName '1.0.5'

        buildConfigField 'int', 'VK_API_APP_ID', getProps("vk_app_id")
        buildConfigField 'String', 'FCM_SENDER_ID', getProps("fcm_sender_id")
        buildConfigField 'boolean', 'FULL_APP', 'true'
        buildConfigField 'String', 'SERVICE_TOKEN', getProps("vk_service_token")
        buildConfigField 'String', 'VK_CLIENT_SECRET', getProps("vk_client_secret")
        buildConfigField 'String', 'YOUTUBE_DEV_KEY', getProps("youtube_dev_key")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        warning 'InvalidPackage'
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled true
            multiDexEnabled false
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            multiDexEnabled false
        }
    }
}

def getProps(String propName) {
    def propsFile = rootProject.file('build-config-fields.properties')

    if (propsFile.exists()) {
        def props = new Properties()

        FileInputStream fis
        try {
            fis = new FileInputStream(propsFile)
            props.load(fis)

            def value = props[propName]
            fis.close()
            return value
        } finally {
            if(fis != null) fis.close()
        }
    } else {
        throw new GradleException('File build-config-fields.properties not found')
    }
}

allprojects {
    repositories {
        maven {
            url "https://jitpack.io"
        }

        maven {
            url 'https://maven.google.com'
        }
    }
}

ext {
    retrofitLibraryVersion = '2.6.2'
    okhttpLibraryVersion = '4.2.2'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'com.google.android.material:material:1.2.0-alpha01'
    implementation 'androidx.core:core:1.2.0-beta01'
    implementation 'androidx.recyclerview:recyclerview:1.1.0-rc01'
    implementation "androidx.viewpager:viewpager:1.0.0"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.0.0"
    implementation "androidx.drawerlayout:drawerlayout:1.0.0"
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.media:media:1.1.0'
    implementation 'androidx.browser:browser:1.2.0-alpha09'
    implementation "androidx.preference:preference:1.1.0"
    implementation "androidx.exifinterface:exifinterface:1.0.0"
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.13'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation files('libs/YouTubeAndroidPlayerApi.jar')
    implementation project(path: ':mvpcore')
    implementation "com.squareup.retrofit2:retrofit:$retrofitLibraryVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitLibraryVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitLibraryVersion"
    implementation "com.squareup.okhttp3:okhttp:$okhttpLibraryVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpLibraryVersion"
    implementation 'com.google.firebase:firebase-core:17.2.1'
    implementation 'com.google.firebase:firebase-database:19.2.0'
    implementation 'com.google.firebase:firebase-messaging:20.0.0'
    releaseImplementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation 'com.google.android.exoplayer:exoplayer-core:2.8.4'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.airbnb.android:lottie:3.1.0'
}

apply plugin: 'com.google.gms.google-services'