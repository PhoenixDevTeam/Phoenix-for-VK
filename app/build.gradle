apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'

android {
    packagingOptions {
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    compileSdkVersion 28

    defaultConfig {
        applicationId "biz.dealnote.messenger"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 50012
        versionName '5.0.0-alpha12'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        warning 'InvalidPackage'
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled true
            multiDexEnabled false
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            multiDexEnabled false
        }
    }

    /*applicationVariants.all { variant ->
        def flavor = variant.mergedFlavor
        def versionCode = flavor.versionCode

        def versionName = flavor.versionName
        if (variant.buildType.isDebuggable()) {
            versionCode += 1
            versionName = versionCode + "-d"
        }

        flavor.versionCode = versionCode
        flavor.versionName = versionName
    }*/

    flavorDimensions "type"

    productFlavors {
        full {
            applicationId "biz.dealnote.phoenix"
            dimension "type"
            buildConfigField 'int', 'VK_API_APP_ID', getProps("full_vk_app_id")
            buildConfigField 'String', 'GCM_SENDER_ID', getProps("full_gcm_sender_id")
            buildConfigField 'boolean', 'FULL_APP', 'true'
            buildConfigField 'String', 'SERVICE_TOKEN', getProps("full_vk_service_token")
            buildConfigField 'String', 'VK_CLIENT_SECRET', getProps("full_vk_client_secret")
            buildConfigField 'String', 'YOUTUBE_DEV_KEY', getProps("full_youtube_dev_key")
        }
        lite {
            applicationId "biz.dealnote.messenger"
            dimension "type"
            buildConfigField 'int', 'VK_API_APP_ID', getProps("lite_vk_app_id")
            buildConfigField 'String', 'GCM_SENDER_ID', getProps("lite_gcm_sender_id")
            buildConfigField 'boolean', 'FULL_APP', 'false'
            buildConfigField 'String', 'SERVICE_TOKEN', getProps("lite_vk_service_token")
            buildConfigField 'String', 'VK_CLIENT_SECRET', getProps("lite_vk_client_secret")
            buildConfigField 'String', 'YOUTUBE_DEV_KEY', getProps("lite_youtube_dev_key")
        }
    }
}

def getProps(String propName) {
    def propsFile = rootProject.file('build-config-fields.properties')

    if (propsFile.exists()) {
        def props = new Properties()

        FileInputStream fis
        try {
            fis = new FileInputStream(propsFile)
            props.load(fis)

            def value = props[propName]
            fis.close()
            return value
        } finally {
            if(fis != null) fis.close()
        }
    } else {
        throw new GradleException('File build-config-fields.properties not found')
    }
}

allprojects {
    repositories {
        maven {
            url "https://jitpack.io"
        }

        maven {
            url 'https://maven.google.com'
        }
    }
}

ext {
    retrofitLibraryVersion = '2.4.0'
    okhttpLibraryVersion = '3.10.0'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "com.google.android.material:material:1.1.0-alpha06"
    implementation "androidx.core:core:1.1.0-beta01"
    implementation "androidx.recyclerview:recyclerview:1.0.0"
    implementation "androidx.viewpager:viewpager:1.0.0"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.0.0"
    implementation "androidx.drawerlayout:drawerlayout:1.0.0"
    implementation "androidx.vectordrawable:vectordrawable:1.0.1"
    implementation "androidx.media:media:1.0.1"
    implementation "androidx.browser:browser:1.0.0"
    implementation "androidx.preference:preference:1.1.0-alpha05"
    implementation "androidx.exifinterface:exifinterface:1.0.0"
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.2'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation files('libs/YouTubeAndroidPlayerApi.jar')
    implementation project(path: ':mvpcore', configuration: 'default')
    implementation "com.squareup.retrofit2:retrofit:$retrofitLibraryVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitLibraryVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitLibraryVersion"
    implementation "com.squareup.okhttp3:okhttp:$okhttpLibraryVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpLibraryVersion"
    implementation "com.google.firebase:firebase-core:16.0.9"
    implementation "com.google.firebase:firebase-database:17.0.0"
    implementation "com.google.firebase:firebase-messaging:18.0.0"
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.0'
    implementation 'com.google.android.exoplayer:exoplayer-core:2.8.4'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
}

apply plugin: 'com.google.gms.google-services'